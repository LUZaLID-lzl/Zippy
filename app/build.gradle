plugins {
    id 'com.android.application'
}

// 从AndroidManifest.xml读取版本信息
def getVersionFromManifest() {
    def manifestFile = file('src/main/AndroidManifest.xml')
    def manifest = new XmlSlurper().parse(manifestFile)
    
    // 获取android命名空间的属性
    def versionCode = manifest.attributes()['android:versionCode']
    def versionName = manifest.attributes()['android:versionName']
    
    return [
        versionCode: versionCode ? versionCode.toInteger() : 1,
        versionName: versionName ? versionName.toString() : "1.0.0"
    ]
}

def versionInfo = getVersionFromManifest()

android {
    namespace 'com.luza.zippy'
    compileSdk 34
    
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }
    
    defaultConfig {
        applicationId "com.luza.zippy"
        minSdk 23
        targetSdk 34
        versionCode versionInfo.versionCode
        versionName versionInfo.versionName
        
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                    "room.schemaLocation": "$projectDir/schemas".toString()
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        viewBinding true
    }
    
    // 自定义APK输出名称
    applicationVariants.all { variant ->
        variant.outputs.all {
            def appName = "Zippy"
            def buildType = variant.buildType.name
            def versionName = variant.versionName ?: "1.0"
            def versionCode = variant.versionCode ?: 1
            
            if (buildType == "debug") {
                outputFileName = "${appName}-debug-v${versionName}-${versionCode}.apk"
            } else if (buildType == "release") {
                outputFileName = "${appName}-release-v${versionName}-${versionCode}.apk"
            }
        }
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.navigation:navigation-fragment:2.6.0'
    implementation 'androidx.navigation:navigation-ui:2.6.0'
    implementation 'androidx.activity:activity:1.8.0'
    implementation 'androidx.room:room-common:2.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'androidx.core:core:1.6.0'  // 包含最新NFC API
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    def room_version = "2.6.1"
    
    // Room dependencies
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"

    // Optional - RxJava2 support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    // Optional - RxJava3 support for Room
    implementation "androidx.room:room-rxjava3:$room_version"

    // Optional - Guava support for Room, including Optional and ListenableFuture
    implementation "androidx.room:room-guava:$room_version"

    // Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    // Paging 3 integration
    implementation "androidx.room:room-paging:$room_version"

    // WorkManager
    def work_version = "2.9.0"
    implementation "androidx.work:work-runtime:$work_version"

    // Glide图片加载库
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.16.0'
}